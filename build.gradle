plugins {
    id 'application'
    id 'maven-publish'
    id 'checkstyle'
    id "com.github.spotbugs" version "3.0.0"
    id 'jacoco'
    id 'org.sonarqube' version '2.8'
    id 'com.jfrog.bintray' version "1.8.4"
}

version = '1.0.6'
group = 'com.asteroid.duck'

application {
    mainClassName = 'CdbRunner'
}

// use maven central to get JARs
repositories {
    mavenCentral()
}

// dependencies
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    testCompile 'junit:junit:4.+'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
}

spotbugs {
    effort = 'max'
    ignoreFailures = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        html {
            enabled = true
        }
        xml {
            enabled = false
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            group project.group
            version project.version
        }
    }
}


sonarqube {
    properties {
        property 'sonar.organization', 'duckasteroid-github'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.login', System.getenv("SONARQUBE_KEY")
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        userOrg = 'duck-asteroid'
        name = project.name
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
    }
    publish = true // Whether version should be auto published after an upload
    override = true // Whether to override version artifacts already published
    publications = ['maven']
}