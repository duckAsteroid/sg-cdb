buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.spotbugs:spotbugs-gradle-plugin:3.0.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: "com.github.spotbugs"
    apply plugin: 'org.sonarqube'

    version = '1.1.1'
    group = 'com.asteroid.duck.sg-cdb'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        // use maven central to get JARs
        mavenCentral()
    }

    // dependencies
    dependencies {
        testCompile 'junit:junit:4.+'
        testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
    }

    spotbugs {
        effort = 'max'
        ignoreFailures = true
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = false
            }
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                group project.group
                version project.version
            }
        }
    }


    sonarqube {
        properties {
            property 'sonar.organization', 'duckasteroid-github'
            property 'sonar.host.url', 'https://sonarcloud.io'
            property 'sonar.login', System.getenv("SONARQUBE_KEY")
        }
    }
}


